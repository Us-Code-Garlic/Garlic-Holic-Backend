plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'com.garlic-holic'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

// 여기서 한 번만 선언
configurations {
    asciidoctorExt
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly    'com.mysql:mysql-connector-j'

    implementation 'org.springframework.boot:spring-boot-starter-mail'

    // Google Cloud Speech
    implementation('com.google.cloud:google-cloud-speech:4.32.0') {
        exclude group: 'io.grpc', module: 'grpc-protobuf-lite'
    }
    implementation 'io.grpc:grpc-protobuf:1.62.2'
    implementation 'com.google.protobuf:protobuf-java:3.25.2'
    implementation 'com.google.auth:google-auth-library-oauth2-http:1.22.0'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    // Asciidoctor용 추가 의존성
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
}

// Rest Docs snippetsDir 설정
ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    useJUnitPlatform()
    outputs.dir snippetsDir
}

asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn test
}

bootJar {
    dependsOn asciidoctor
    from("build/docs/asciidoc") {
        into("static/docs")
    }
}